apply plugin: 'com.android.application'

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "razerdp.friendcircle"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        renderscriptTargetApi 25
        renderscriptSupportModeEnabled true

        if (isModule.toBoolean()) {
            buildConfigField 'boolean', 'isModule', 'true'
        } else {
            buildConfigField 'boolean', 'isModule', 'false'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //jni
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            jni.srcDirs = [] //disable automatic ndk-build call
        }

    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    //图片选择模块尚未完成，所以暂时不需要分开依赖
    if (!isModule.toBoolean()) {
        implementation project(':circle_base_library')
        implementation project(':circle_base_ui')
        implementation project(':circle_photoselect')
        implementation project(':circle_publish')
        annotationProcessor rootProject.ext.dependencies.arouter_compiler

    } else {
        implementation project(':circle_base_library')
        implementation project(':circle_base_ui')
        implementation project(':circle_common')
    }
}
